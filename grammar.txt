List[item] -> "\n"? (item (("\n" item)+ | ("," "\n"? item)* ","?) "\n"?)?
List2[item1, item2]
           -> "\n"? item1 ( ("\n" item1)* "\n" item2 ("\n" item2)* "\n"?
                          | ("," "\n"? item1)* "," "\n"? item2 ("," "\n"? item2)* ","? "\n"?)
            | List[item1]
            | List[item2]
Delimited[item]
           -> "(" List[item] ")" | item
Delimited2[item1, item2]
           -> "(" List2[item1, item2] ")" | item1 | item2
Left[op, operand]
           -> operand (op operand)*
Right[op, operand]
           -> operand (op Right[op, operand])?

program    -> List[expression] EOF
expression -> assignment
            | keyword
            | lambda
            | declaration
            | boolor
assignment -> Delimited[target] ASSIGN expression
target     -> "const"? typehint? IDENTIFIER
typehint   -> "<" type ">"
type       -> IDENTIFIER ("[" List[type] "]")?
keyword    -> if | case | try | for | while
            | iter | do | object | enum | module | exception | mutable
            | throw | yield | yieldfrom
if         -> "if" expression "then" expression ("else" expression)?
case       -> "case" primary "in" mapping ("else" expression)?
try        -> "try" expression ("catch" IDENTIFIER ("as" IDENTIFIER)? expression)+
for        -> "for" Delimited[IDENTIFIER] "in" expression block
while      -> "while" expression block
iter       -> "iter" (list | for | while)
do         -> "do" block
object     -> "object" block
enum       -> "enum" "flags"? "{" List[enumitem] "}"
enumitem   -> IDENTIFIER ("=" NUMBER)?
module     -> "module" block
exception  -> "exception" block
mutable    -> "mutable" (object | mapping | list | tuple | STRING)
throw      -> "throw" expression
raises     -> "raises" "(" expression "," IDENTIFIER ")"
yieldfrom  -> "yield" "from" expression
yield      -> "yield" expression
lambda     -> Delimited2[vparam, kwparam] "->" expression
vparam     -> "*"? typehint IDENTIFIER
kwparam    -> "**" typehint IDENTIFIER | typehint IDENTIFIER ":" expression
declaration-> "const"? typehint IDENTIFIER
boolor     -> Right["or", boolxor]      # any() - returns the first truthy argument, else the last
boolxor    -> Right["xor", booland]     # parity()
booland    -> Right["and", inclusion]   # all() - return the first falsey argument, else the last
inclusion  -> Right[("in" | "not" "in"), identity]
identity   -> Right[("is" | "is" "not"), comparison]
comparison -> Right[comp, bitor]        # comp()
bitor      -> Left["|", bitxor]         # bitor()
bitxor     -> Left["^", bitand]         # bitxor()
bitand     -> Left["&", shift]          # bitand()
shift      -> Left[("<<" | ">>"), add]  # shift()
addition   -> Left[("+" | "-"), mult]   # sum()
product    -> Left[("*" | "/"), modulus]# product()
modulus    -> Left["%", exponent]       # mod()
exponent   -> Right["**", unary]        # power()
unary      -> ("not" | "!" | "-") unary # inv(), bitinv(), neg()
            | primary
primary    -> atom ("." IDENTIFIER | "(" List2[varg, kwarg] ")" | list)*
varg       -> "*"? expression
kwarg      -> "**" expression | IDENTIFIER ":" expression
atom       -> mapping | block | list | grouping | tuple | literal | IDENTIFIER
mapping    -> "{" List[pair] "}"
pair       -> expression ":" expression
block      -> "{" List[expression] "}"
list       -> "[" (range | List[expression]) "]"
range      -> primary ".." primary? ("," primary)?
grouping   -> "(" expression ")"
tuple      -> "(" List[expression] ")"
literal    -> STRING | NUMBER | "true" | "false" | "none" | "break" | "continue" | "pass"
